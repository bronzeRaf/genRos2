/*
  Generos DSL grammar.
*/

GenerosModel:
	commands *= Command
	/*packages*=Package
	nodes*=Node
	parameters*=Parameter
	publishers*=Publisher
	
	clients*=Client
	servers*=Server
	dependencies*=Dependency
	messages*=Message
	subscribers*=Subscriber
*/
;

Command:
	Package | Node | Parameter | Publisher | Subscriber | Client | Server | ActionServer | ActionClient | Dependency | Message | ServiceMessage | ActionInterface | QoSProfile
;

Package:
    'package' name=ID '{'
		'path' '=' packagePath=STRING
		'license' '=' license = STRING
		'maintainer' '=' maintainer = STRING
		'email' '=' email = STRING
		('builtin' '=' builtin=BOOL)?
		('hasNode' '=' hasNodes *= [Node][eolterm])?
		('hasDependency' '=' hasDependencies *= [Dependency][eolterm])?		
    '}'
;

Node:
    'node' name=ID '{'
		('namespace' '=' namespace=STRING)?
		('hasParameter' '=' hasParameters *= [Parameter][eolterm])?
		('hasPublisher' '=' hasPublishers *= [Publisher][eolterm])?
		('hasSubscriber' '=' hasSubscribers *= [Subscriber][eolterm])?
		('hasServer' '=' hasServers *= [Server][eolterm])?
		('hasClient' '=' hasClients *= [Client][eolterm])?
		('hasActionServer' '=' hasActionServers *= [ActionServer][eolterm])?
		('hasActionClient' '=' hasActionClients *= [ActionClient][eolterm])?
    '}'
;

Parameter:
    'parameter' name=ID '{'
		'type' '=' type=STRING
		'value' '=' value=STRING
		('description' '=' description=STRING)?
    '}'
;

Publisher:
    'publisher' name=ID '{'
		'topicPath' '=' topicPath=STRING
		'publishRate' '=' publishRate=FLOAT
		'message' '=' message=[Message]
		('qos' '=' qos=[QoSProfile])?
    '}'
;

Subscriber:
    'subscriber' name=ID '{'
		'topicPath' '=' topicPath=STRING
		'message' '=' message=[Message]
		('qos' '=' qos=[QoSProfile])?
    '}'
;

Server:
    'server' name=ID '{'
		'servicePath' '=' servicePath=STRING
		'serviceName' '=' serviceName=STRING
		'service' '=' service=[ServiceMessage]
		('qos' '=' qos=[QoSProfile])?
    '}'
;

Client:
    'client' name=ID '{'
		'servicePath' '=' servicePath=STRING
		'serviceName' '=' serviceName=STRING
		'service' '=' service=[ServiceMessage]
		('qos' '=' qos=[QoSProfile])?
    '}'
;

ActionServer:
    'actionServer' name=ID '{'
		'action' '=' actioninterface=[ActionInterface]
    '}'
;

ActionClient:
    'actionClient' name=ID '{'
		'action' '=' actioninterface=[ActionInterface]
    '}'
;

Dependency:
    'dependency' name=ID '{'
		'package' '=' package=[Package]
    '}'
;

Message:
   CustomMessage | RosMessage
;

RosMessage:
    'message' name=ID 'package' '=' package=STRING
;

CustomMessage:
    'message' name=ID '{'
		('description' '=' description=STRING)?
		hasObjects *= Object
    '}'
;
ServiceMessage:
   CustomServiceMessage | RosServiceMessage
;

RosServiceMessage:
    'service' name=ID 'package' '=' package=STRING
;

CustomServiceMessage:
    'service' name=ID '{'
		'request' ':' request = Request
		'response' ':' response = Response
		
		('description' '=' description=STRING)?
		
    '}'
;

Request: 
	hasObjects *= Object
;
Response: 
	hasObjects *= Object
;

ActionInterface:
   CustomActionInterface
;

CustomActionInterface:
    'action' name=ID '{'
		'goal' ':' goal = Goal
		'result' ':' result = Result
		'feedback' ':' feedback = Feedback
		
		('description' '=' description=STRING)?
		
    '}'
;

Goal: 
	hasObjects *= Object
;
Result: 
	hasObjects *= Object
;
Feedback: 
	hasObjects *= Object
;

Object:
	(Primitive | ROSData)
;


Primitive:
	'prim' type=ID ','
		name=ID
		(',' 'description''=' description=STRING)? 
		(',' 'constant''=' constant=BOOL)? 
		(',' 'default''=' default=STRING)?
;

ROSData: 
	'ros' type=ID ',' 
	name=ID ',' 
	package=ID 
	(',' 'description''=' description=STRING)? 
	(',' 'constant''=' constant=BOOL)? 
	(',' 'default''=' default=STRING)?
;

QoSProfile:
	CustomQosProfile | RosQosProfile
;

CustomQosProfile: 
	'qosprofile' name=ID '{'
		('history' '=' history=STRING)?
		('depth' '=' depth=INT)?
		('reliability' '=' reliability=STRING)?
		('durability' '=' durability=STRING)?
		('livelines' '=' livelines=STRING)?
		('deadlineSec' '=' deadlineSec=INT)?
		('deadlineNSec' '=' deadlineNSec=INT)?
		('lifespanSec' '=' lifespanSec=INT)?
		('lifespanNSec' '=' lifespanNSec=INT)?
		('liveliness_lease_durationSec' '=' liveliness_lease_durationSec=INT)?
		('liveliness_lease_durationNSec' '=' liveliness_lease_durationNSec=INT)?
		('avoid_ros_namespace_conventions' '=' avoid_ros_namespace_conventions=BOOL)?
	'}'
;

RosQosProfile:
	'presetqos' name=ID
;

// Special rule for comments. Comments start with //
Comment:
    /\/\/.*$/
;
